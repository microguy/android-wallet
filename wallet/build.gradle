apply plugin: 'com.android.application'
apply plugin: 'androidsvgdrawable'

repositories {
    mavenLocal()
}

configurations {
    all*.exclude group: 'com.google.android', module: 'android'
    all*.exclude group: 'androidx.legacy', module: 'legacy-support-core-ui'
    all*.exclude group: 'androidx.legacy', module: 'legacy-support-core-utils'
}

dependencies {
    implementation project(':integration-android')
    implementation 'androidx.annotation:annotation:1.0.0'
    implementation 'androidx.core:core:1.0.0'
    implementation 'androidx.fragment:fragment:1.3.0'
    implementation 'androidx.legacy:legacy-support-core-utils:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.viewpager:viewpager:1.0.0'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation "androidx.sqlite:sqlite:2.0.0"
    implementation "androidx.room:room-runtime:2.0.0"
    implementation 'androidx.activity:activity:1.3.1'
    annotationProcessor "androidx.room:room-compiler:2.0.0"
    implementation 'org.goldcoinj:goldcoinj-core:0.14.10'
    implementation 'com.google.protobuf:protobuf-java:3.6.1'
    implementation 'com.google.guava:guava:25.1-android'
    implementation 'com.google.zxing:core:3.3.3'
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.1'
    implementation 'com.squareup.moshi:moshi:1.6.0'
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'com.github.tony19:logback-android:1.3.0-2'
    testImplementation 'junit:junit:4.12'
}

ext {
    archivesBaseName = 'goldcoin-wallet'
}

android {
    compileSdkVersion 35
    buildToolsVersion '33.0.2'

    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 35
        applicationId 'de.schildbach.wallet_test'
        generatedDensities = ['hdpi', 'xhdpi']
        ndkVersion "23.1.7779620"

        dexOptions {
            preDexLibraries false
        }

        ndk {
            abiFilters 'armeabi-v7a','arm64-v8a','x86','x86_64'
        }

        externalNativeBuild {
            cmake {
                cFlags '-DHAVE_CONFIG_H'
            }
        }
    }

    buildTypes {
        all {
            minifyEnabled true
            proguardFile 'proguard.cfg'
        }
        debug {
            debuggable true
        }
    }
    defaultConfig {
        versionCode 64109
        versionName "6.41.5"
        vectorDrawables.useSupportLibrary = true
    }
    flavorDimensions("version")
    productFlavors {
        _testNet {
            dimension "version"
            applicationId = "de.schildbach.wallet.goldcoin_test"
        }
        prod {
            dimension "version"
            applicationId = "de.schildbach.wallet.goldcoin"
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }
        test {
            java.srcDirs = ['test']
            resources.srcDirs = ['test']
        }
    }

    externalNativeBuild {
        cmake {
            path 'cpp/CMakeLists.txt'
        }
    }

    packagingOptions {
        exclude 'META-INF/*.version'
        exclude 'META-INF/proguard/**'
        exclude 'lib/x86_64/darwin/libscrypt.dylib'
        exclude 'okhttp3/internal/publicsuffix/publicsuffixes.gz'
        exclude 'org/bitcoinj/crypto/mnemonic/wordlist/english.txt'
        exclude 'org/bitcoinj/crypto/cacerts'
        exclude 'org.bitcoin.production.checkpoints.txt'
        exclude 'org.bitcoin.test.checkpoints.txt'
    }
}

task svgToPngMipmap(type: fr.avianey.androidsvgdrawable.gradle.SvgDrawableTask) {
    from = files('graphics/mipmap')
    to = file('res')
    targetedDensities = ['hdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
    outputFormat = 'PNG'
    outputType = 'mipmap'
}